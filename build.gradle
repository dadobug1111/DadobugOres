plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false
    id 'io.github.juuxel.loom-quiltflower' version '1.7.1' apply false
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
    id "io.github.pacifistmc.forgix" version "1.2.9"
}

architectury {
    minecraft = rootProject.minecraft_version
}

forgix {
    group = "no.dadobug.oremod"
    mergedJarName = rootProject.archives_base_name + "-merged-" + rootProject.minecraft_version + "-" + rootProject.mod_version + ".jar"
    outputDir = "build/libs/merged"
    //noinspection GroovyAssignabilityCheck
    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/" + rootProject.archives_base_name + "-" + rootProject.mod_version + "-fabric.jar"
    }

    //noinspection GroovyAssignabilityCheck
    forge {
        projectName = "forge"
        jarLocation = "build/libs/" + rootProject.archives_base_name + "-" + rootProject.mod_version + "-forge.jar"
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower"
    apply plugin: "com.matthewprenger.cursegradle"
    apply plugin: "com.modrinth.minotaur"

    loom {silentMojangMappingsLicense()}

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        //mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        //mappings

        mappings loom.layered() {
            it.parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment_version}")
            it.officialMojangMappings {
                setNameSyntheticMembers(false)
            }
        }
    }
    build.finalizedBy(mergeJars)
    assemble.finalizedBy(mergeJars)
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group


    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.

        maven { url 'https://raw.githubusercontent.com/SolidBlock-cn/mvn-repo/main'}
        maven { url "https://maven.terraformersmc.com/releases/" }

        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.parchmentmc.org" }
        maven {
            url = "https://www.cursemaven.com"
            allowInsecureProtocol = true
        }
        maven { url = "https://mvn.devos.one/snapshots/" }
        maven { url = "https://jitpack.io/" }


    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
